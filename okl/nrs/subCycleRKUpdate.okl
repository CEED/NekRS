/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */
@kernel void subCycleInitU0(const dlong N,
                  const dlong Nfields,
                  const dlong fieldOffset,
                  const dlong torder,
                  const dlong nEXT,
                  const dlong toffset,
                  const dlong offset,
                  const dfloat coef,
                  @restrict const dfloat* massMatrix,
                  @restrict const dfloat *S,
                  @restrict       dfloat *BS){
  for(dlong n=0;n<N;++n;@tile(p_blockSize,@outer,@inner)){
    if(torder == (nEXT-1)){
      for(int fld = 0; fld < Nfields; ++fld){
        if(p_MovingMesh){
          BS[n + fld * fieldOffset] = 
            coef * massMatrix[n + offset] * S[n + toffset + fld * fieldOffset];
        } else {
          BS[n + fld * fieldOffset] = 
            coef * S[n + toffset + fld * fieldOffset];
        }
      }
    } else {
      for(int fld = 0; fld < Nfields; ++fld){
        if(p_MovingMesh){
          BS[n + fld * fieldOffset] += coef * massMatrix[n + offset] * S[n + toffset + fld * fieldOffset];
        } else {
          BS[n + fld * fieldOffset] += coef * S[n + toffset + fld * fieldOffset];
        }
      }
    }
  }
}
@kernel void subCycleRK(const dlong N,
                  const dlong Nfields,
                  const dlong offset,
                  const dfloat sdt,
                  @restrict const dfloat* rkb,
                  @restrict const dfloat* r1,
                  @restrict const dfloat* r2,
                  @restrict const dfloat* r3,
                  @restrict const dfloat* r4,
                  @restrict       dfloat *y){

  for(dlong n=0;n<N;++n;@tile(p_blockSize,@outer,@inner)){
    for(int fld = 0; fld < Nfields; ++fld){
      const dlong fieldOffset = fld * offset;
      dfloat sn = 0.0;
      sn -= sdt * rkb[0] * r1[n + fieldOffset];
      sn -= sdt * rkb[1] * r2[n + fieldOffset];
      sn -= sdt * rkb[2] * r3[n + fieldOffset];
      sn -= sdt * rkb[3] * r4[n + fieldOffset];
      y[n + fieldOffset] += sn;
    }
  }
}
@kernel void subCycleERKUpdate(const dlong N,
                                  const int stage,
                                  const dfloat dt,
                                  const dlong offset,
                                  @restrict const dfloat* rka,
                                  @restrict const dfloat* rkb,
                                  @restrict dfloat*  Us, // U0 at Tn
                                  @restrict dfloat*  RHSU,
                                  @restrict dfloat*  U)
{
  for(dlong id = 0; id < N; ++id; @tile(p_blockSize,@outer,@inner)) {
        if(stage == 3) { // Final stage
#pragma unroll p_NVfields
          for (int fld = 0; fld < p_NVfields; fld++) {
            dfloat un = Us[id + fld * offset];
            for(int st = 0; st < stage + 1; st++) {
              un -= dt * rkb[st] * RHSU[id + fld * offset + st * p_NVfields * offset]; // k0,k1,k2,k3
            }
            U[id + fld * offset] = un;
          }
        } else{
#pragma unroll p_NVfields
          for (int fld = 0; fld < p_NVfields; fld++) {
            dfloat rhsu = RHSU[id + fld * offset + stage * p_NVfields * offset];
            dfloat un   = Us[id + fld * offset];
            // Intermediate stage Update
            U[id + fld * offset] = un - dt * rka[stage + 1] * rhsu;
          }
        }
  }
}